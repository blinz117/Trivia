plugins {
    id 'com.gladed.androidgitversion' version '0.4.9'
    id("io.gitlab.arturbosch.detekt").version("1.0.0-RC15")
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "androidx.navigation.safeargs.kotlin"

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.4"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

androidGitVersion {
    format "%tag%%-branch%%-count%%-dirty%"
    baseCode 1
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "dev.bryanlindsey.trivia"
        minSdkVersion 23
        targetSdkVersion 28

        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    jacoco {
        version = "0.8.4"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'org.koin:koin-android-viewmodel:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.activity:activity-ktx:1.0.0-beta01'
    implementation 'androidx.fragment:fragment-ktx:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'

    def nav_version = "2.1.0-alpha05"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha01"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.0'

    def hyperionVersion = '0.9.27'
    debugImplementation "com.willowtreeapps.hyperion:hyperion-core:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-build-config:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-timber:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-disk:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-shared-preferences:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-recorder:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-attr:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-measurement:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-phoenix:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-crash:$hyperionVersion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-geiger-counter:$hyperionVersion"
    debugImplementation 'pl.droidsonroids.foqa:deviceinfo:0.0.10'

    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-config:17.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.1.0-beta01'

    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.12.0'

    implementation 'androidx.preference:preference-ktx:1.1.0-beta01'
}

detekt {
    filters = ".*/resources/.*,.*/build/.*"

    // As of RC15, this value doesn't work yet. It works on latest master, but a build hasn't been publicly released yet
    ignoreFailures = true
    
    config = files("default-detekt-config.yml")
}

/**
 * Adapted from https://medium.com/@rafael_toledo/unified-code-coverage-for-android-revisited-44789c9b722f
 */
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*Fragment.*',
            '**/*Activity.*'
    ]
    // generated classes
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes
    )

    // sources
    sourceDirectories = files([
            android.sourceSets.main.java.srcDirs,
            "$project.projectDir/src/main/java"
    ])
    executionData = fileTree(dir: project.buildDir, includes: ['jacoco/testDebugUnitTest.exec'])
}

apply plugin: 'com.google.gms.google-services'
